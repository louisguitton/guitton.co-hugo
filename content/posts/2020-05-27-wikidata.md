---
slug: wikidata
title: Learn SPARQL in 5 minutes and use it to query WikiData
date: 2020-05-27
lastmod: 2020-05-27
summary: >
  Learn how to fomulate questions to WikiData using SPARQL.
categories:
  - Data
featuredImage: /images/wikidata.png
---

I'm working on Entity Linking and Knowledge Bases.
In that context, exporting a relevant part of Wikidata can be really useful to build surface form dictionaries and coocurence probabilities etc...
In order to know which part of Wikidata is relevant to dump, I thought we could query Wikidata (although it seems we can only download the entire dump and filter afterwards).

`SPARQL` is a language to formulate questions (queries) for knowledge databases.
Therefore you can query Wikidata with `SPARQL`.
At first sight, the syntax is not particularly easy and I've gone through this [tutorial](https://m.wikidata.org/wiki/Special:MyLanguage/Wikidata:SPARQL_tutorial).

## Notes

- `#`is the comment character
- The `SELECT` clause lists variables that you want returned (variables start with a question mark)
- The `WHERE` clause contains restrictions on them, in the form of SPO triples (_subject_, _predicate_, _object_), e.g. `?fruit hasColor yellow.`
- On Wikidata, items and properties are not identified by human-readable names like “hasColor” (property) or “yellow” (item).
- Instead, Wikidata items and properties are assigned an identifier, that you need to know beforehand.
  - for items, it's a Q-number, e.g. "yellow" is [Q943](https://m.wikidata.org/wiki/Q943)
  - for properties, it's a P-number, e.g. "hasColor" is [P462](https://m.wikidata.org/wiki/Property:P462)
- you can [search](https://m.wikidata.org/wiki/Special:Search) for the itentifiers using `search term` for items and `P:search term` for properties
- but you should rely on **autocompletion** in [query.wikidata.org](https://query.wikidata.org/) by pressing <kbd>Ctrl</kbd> + <kbd>Space</kbd>
- Finally, you need to include prefix namespaces to query the WQDS (Wikidata Query Service). There are many prefixes, one for each namespace in SPARQL :
  - `wd:` for items
  - `wdt:` for properties, pointing to the object
- to get more than the Wikidata ID as selectable attributes, you need to include them in the WHERE clause using `SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }`

Putting this all together we get:

```sparql
SELECT ?fruit ?fruitLabel
WHERE
{
  # fruit hasColor yellow
  ?fruit wdt:P462 wd:Q943
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
}
```

[Try it](https://query.wikidata.org/#SELECT%20%3Ffruit%20%3FfruitLabel%0AWHERE%0A%7B%0A%20%20%23%20fruit%20hasColor%20yellow%0A%20%20%3Ffruit%20wdt%3AP462%20wd%3AQ943%0A%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22%5BAUTO_LANGUAGE%5D%22.%20%7D%0A%7D)

- You can further filter this down by adding more triple conditions using `;` character
e.g. you could filter for actual fruits by doing

```sparql
# fruit instance of or subclass of a fruit
?fruit wdt:P31/wdt:P279* wd:Q3314483;
```

- Advanced filters :
  - `p:` for properties, pointing to the subject
  - `ps:` for property statement
  - `pq:` for property qualifier
- You can abbreviate a lot with the `[]` syntax

```sparql
SELECT ?painting ?paintingLabel ?material ?materialLabel
WHERE
{
  # element is a painting
  ?painting wdt:P31/wdt:P279* wd:Q3305213;
  # extract the statement node 'material' (P186)
            p:P186 [
              # get material property statement
              ps:P186 ?material;
              # 'applies to part'(P518) 'painting surface'(Q861259)
              pq:P518 wd:Q861259
            ].
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
}
```

- More grammar by example: `ORDER BY`, `LIMIT`

```sparql
SELECT ?country ?countryLabel ?population
WHERE
{
  # instances of sovereign state
  ?country wdt:P31/wdt:P279* wd:Q3624078;
  # hasPopulation populationValue
           wdt:P1082 ?population.
  # filter for english translations
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
# ASC(?something) or DESC(?something)
ORDER BY DESC(?population)
LIMIT 10
```

- If you add more variables like population above, the query will filter out
countries that don't have a population value. To fix this, use an `OPTIONAL` clause

```sparql
SELECT ?book ?title ?illustratorLabel ?publisherLabel ?published
WHERE
{
  ?book wdt:P50 wd:Q35610.
  OPTIONAL { ?book wdt:P1476 ?title. }
  OPTIONAL { ?book wdt:P110 ?illustrator. }
  OPTIONAL { ?book wdt:P123 ?publisher. }
  OPTIONAL { ?book wdt:P577 ?published. }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
}
```

- `FILTER` and `BIND`, see [tutorial section](https://m.wikidata.org/wiki/Wikidata:SPARQL_tutorial#Expressions,_FILTER_and_BIND) for more details

```sparql
SELECT ?person ?personLabel ?age
WHERE
{
  # instance of human
  ?person wdt:P31 wd:Q5;
          wdt:P569 ?born;
          wdt:P570 ?died;
  # died from capital punishment
          wdt:P1196 wd:Q8454.
  BIND(?died - ?born AS ?ageInDays).
  BIND(?ageInDays/365.2425 AS ?ageInYears).
  BIND(FLOOR(?ageInYears) AS ?age).
  FILTER(?age > 90)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
}
```

[Try it](https://query.wikidata.org/#SELECT%20%3Fperson%20%3FpersonLabel%20%3Fage%0AWHERE%0A%7B%0A%20%20%23%20instance%20of%20human%0A%20%20%3Fperson%20wdt%3AP31%20wd%3AQ5%3B%0A%20%20%20%20%20%20%20%20%20%20wdt%3AP569%20%3Fborn%3B%0A%20%20%20%20%20%20%20%20%20%20wdt%3AP570%20%3Fdied%3B%0A%20%20%23%20died%20from%20capital%20punishment%0A%20%20%20%20%20%20%20%20%20%20wdt%3AP1196%20wd%3AQ8454.%0A%20%20BIND%28%3Fdied%20-%20%3Fborn%20AS%20%3FageInDays%29.%0A%20%20BIND%28%3FageInDays%2F365.2425%20AS%20%3FageInYears%29.%0A%20%20BIND%28FLOOR%28%3FageInYears%29%20AS%20%3Fage%29.%0A%20%20FILTER%28%3Fage%20%3E%2090%29%0A%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22%5BAUTO_LANGUAGE%5D%22.%20%7D%0A%7D)

- One can select based on a list of items using `VALUES`

```sparql
SELECT ?item ?itemLabel ?mother ?motherLabel
WHERE {
  # A. Einstein or J.S. Bach
  VALUES ?item { wd:Q937 wd:Q1339 }
  # mother of
  OPTIONAL { ?item wdt:P25 ?mother. }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
```

- The [Label Service extension](https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Label_service) automatically generates labels as follows:
  - `?xxxLabel` as a shortcut for `rdfs:label`
  - `?xxxAltLabel` as a shortcut for `skos:altLabel`
  - `?xxxDescription` as a shortcut for `schema:description`

## Fun SPARQL queries related to football

```sparql
# get the dutch nicknames from Bayern München
SELECT ?item ?itemLabel ?itemDescription ?itemAltLabel
WHERE {
  VALUES ?item { wd:Q15789 }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "nl". }
}
```

## Solutions to the tutorial exercices

Here are my solution to the exercises in that tutorial.

### Chemical elements

Write a query that returns all chemical elements with their element symbol and atomic number, in order of their atomic number.

```sparql
SELECT ?element ?elementLabel ?symbol ?atomic_number
WHERE
{
  ?element wdt:P31 wd:Q11344;
           wdt:P246 ?symbol ;
           wdt:P1086 ?atomic_number .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
}
ORDER BY ASC(?atomic_number)
```

[Try it](https://query.wikidata.org/#SELECT%20%3Felement%20%3FelementLabel%20%3Fsymbol%20%3Fatomic_number%0AWHERE%0A%7B%0A%20%20%3Felement%20wdt%3AP31%20wd%3AQ11344%3B%0A%20%20%20%20%20%20%20%20%20%20%20wdt%3AP246%20%3Fsymbol%20%3B%0A%20%20%20%20%20%20%20%20%20%20%20wdt%3AP1086%20%3Fatomic_number%20.%0A%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22%5BAUTO_LANGUAGE%5D%22.%20%7D%0A%7D%0AORDER%20BY%20ASC%28%3Fatomic_number%29)

### Rivers that flow into the Mississippi

Write a query that returns all rivers that flow directly or indirectly into the Mississippi River.

```sparql
SELECT ?river ?riverLabel
WHERE
{
  ?river wdt:P31 wd:Q4022;
         wdt:P403/wdt:P403* wd:Q1497 .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
}
ORDER BY ASC(?riverLabel)
```

[Try it](https://query.wikidata.org/#SELECT%20%3Friver%20%3FriverLabel%0AWHERE%0A%7B%0A%20%20%3Friver%20wdt%3AP31%20wd%3AQ4022%3B%0A%20%20%20%20%20%20%20%20%20wdt%3AP403%2Fwdt%3AP403%2a%20wd%3AQ1497.%0A%20%20SERVICE%20wikibase%3Alabel%20%7B%20bd%3AserviceParam%20wikibase%3Alanguage%20%22%5BAUTO_LANGUAGE%5D%22.%20%7D%0A%7D%0AORDER%20BY%20ASC%28%3FriverLabel%29%0A%0A%0A)

### References to Le Figaro website

```sparql
SELECT ?ref ?refURL WHERE {
  ?ref pr:P854 ?refURL .
  FILTER (CONTAINS(str(?refURL),'lefigaro.fr')) .
} LIMIT 10
```

[Try it](https://query.wikidata.org/#SELECT%20%3Fref%20%3FrefURL%20WHERE%20%7B%0A%20%20%3Fref%20pr%3AP854%20%3FrefURL%20.%0A%20%20FILTER%20%28CONTAINS%28str%28%3FrefURL%29%2C%27lefigaro.fr%27%29%29%20.%20%20%20%20%20%20%20%0A%7D%20LIMIT%2010)
